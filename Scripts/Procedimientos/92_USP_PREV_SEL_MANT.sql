USE [DB_AHSECO]
GO

CREATE OR ALTER PROCEDURE [dbo].[USP_PREV_SEL_MANT]	
(
/*=======================================================================================================
	Nombre:				Fecha:			Descripcion:
	Diego Bazalar		21.11.24		Realiza el select MAIN para la bandeja de preventivos. 
	EXEC [USP_PREV_SEL_MANT] 1
  =======================================================================================================*/
  @IsNumMant BIGINT
)
AS
BEGIN
SET NOCOUNT ON 
	--Cabecera de Mant
	DECLARE @Sql NVARCHAR(MAX)

	IF OBJECT_ID('tempdb..#tmpCabecera') IS NOT NULL 
		DROP TABLE #tmpCabecera
	IF OBJECT_ID('tempdb..#tmpMantPrev') IS NOT NULL 
		DROP TABLE #tmpMantPrev
	IF OBJECT_ID('tempdb..#tmpMantenimientos') IS NOT NULL 
		DROP TABLE #tmpMantenimientos
	IF OBJECT_ID('tempdb..#tmpCuerpoEquipo') IS NOT NULL 
		DROP TABLE #tmpCuerpoEquipo

	CREATE TABLE #tmpMantPrev
	(
		ID_MANT BIGINT
		,TOTALPREVE INT
		,PENDIENTES INT
		,COMPLETADOS INT
	)


/****Obtenemos el número de preventivos *********************************************************************/
	;WITH CTE_1 As (
		SELECT
			COUNT(MANTDET.ID_MANT) TOTALPREVE
			,MANTDET.ID_MANT
		FROM [dbo].[TBM_MANT_PREV] MANT
		LEFT JOIN [dbo].[TBD_MANT_PREV] MANTDET ON MANT.ID_MANT = MANTDET.ID_MANT
		WHERE MANT.ID_MANT = @IsNumMant
		GROUP BY MANTDET.ID_MANT
		),
	CTE_2 AS (
		SELECT
			COUNT(MANTDET.ID_MANT) PENDIENTES
			,MANTDET.ID_MANT
		FROM [dbo].[TBM_MANT_PREV] MANT
		LEFT JOIN [dbo].[TBD_MANT_PREV] MANTDET ON MANT.ID_MANT = MANTDET.ID_MANT AND MANTDET.ESTADO != 'COM'
		WHERE MANT.ID_MANT = @IsNumMant
		GROUP BY MANTDET.ID_MANT
	),
	CTE_3 AS (																						----------------------------------
		SELECT
			COUNT(MANTDET.ID_MANT) COMPLETADOS
			,MANTDET.ID_MANT
		FROM [dbo].[TBM_MANT_PREV] MANT
		LEFT JOIN [dbo].[TBD_MANT_PREV] MANTDET ON MANT.ID_MANT = MANTDET.ID_MANT 
		WHERE MANTDET.ESTADO = 'COM' AND MANT.ID_MANT = @IsNumMant
		GROUP BY MANTDET.ID_MANT
	)
	INSERT INTO #tmpMantPrev(ID_MANT, TOTALPREVE, PENDIENTES, COMPLETADOS)
	SELECT
		MANT.ID_MANT
		,ISNULL(TOTALPREVE,0)  TOTALPREVE
		,ISNULL(PENDIENTES,0)  PENDIENTES
		,ISNULL(COMPLETADOS,0) COMPLETADOS
	FROM [dbo].[TBM_MANT_PREV] MANT
	LEFT JOIN CTE_1 cte1 ON cte1.ID_MANT = MANT.ID_MANT
	LEFT JOIN CTE_2 cte2 ON cte2.ID_MANT = MANT.ID_MANT
	LEFT JOIN CTE_3 cte3 ON cte3.ID_MANT = MANT.ID_MANT
	WHERE MANT.ID_MANT = @IsNumMant
/**************************************************************************************************************/

/******  Obtenemos la próxima fecha de mantenimiento   ********************************************************/
	SELECT
			ROW_NUMBER() OVER(ORDER BY SERIE) AS CONTADOR
			,SERIE
			,FECHAINSTALACION
			,CONVERT(VARCHAR(10),MIN(FECHAMANTENIMIENTO),103) PROXFECHAMANT
	INTO #tmpProxavencer
	FROM TBM_MANT_PREV MANT
	LEFT JOIN [dbo].[TBD_MANT_PREV] MANTDET ON MANT.ID_MANT = MANTDET.ID_MANT AND ESTADO = 'PEND'
	WHERE MANT.ID_MANT = @IsNumMant
	GROUP BY SERIE,FECHAINSTALACION
/***************************************************************************************************************/

/******Obtenemos la cabecera **********************************************************************************/
	SELECT 
			MANT.ID_MANT				AS ID_MANT
			,MANT.SERIE					AS SERIE
			,MANT.FECHAINSTALACION		AS FECHAINSTALACION
			,SOL.RAZONSOCIAL			AS RAZONSOCIAL	
			,MANT.DIRECCION				AS DIRECCION
			,MANT.UBIGEODEST			AS UBIGEODEST
			,COTDET.CODITEM				AS CODITEM
			,DATOS.VALOR1				AS GARANTIA
			,DATEADD(month,CAST((SUBSTRING(TRIM(DATOS.VALOR1),0,CHARINDEX(' ',DATOS.VALOR1,0))) AS INT) ,DESP.FECHAINSTALACION) AS FECHAVENCIMIENTOGAR
			,COTDET.DESCRIPCION			AS DESCRIPCION
			,MANT.NUMPROCESO			AS NUMPROCESO
			,MANT.TIPOPROCESO			AS TIPOPROCESO
			,MANT.ORDENCOMPRA			AS ORDENCOMPRA
			,COTCOST.CODCICLOPREVENT	AS PERIODO
			,MANT.NUMFIANZA				AS NUMFIANZA
			,COTDESP.CODGARANADIC		AS GARANTIAADIC
		INTO #tmpCabecera
		FROM [dbo].[TBM_MANT_PREV] MANT WITH(NOLOCK)
		LEFT JOIN [dbo].[TBD_DESPACHO_DIST] DESP WITH(NOLOCK) ON MANT.SERIE = DESP.NUMSERIE
		LEFT JOIN [dbo].[TBD_COTIZACIONCOSTOS] COTCOST WITH(NOLOCK) ON DESP.ID_COTDETALLE = COTCOST.ID_COTDETALLE
		LEFT JOIN [dbo].[TBD_COTIZACIONVENTA] COTDET WITH(NOLOCK) ON COTDET.ID = COTCOST.ID_COTDETALLE
		LEFT JOIN [dbo].[TBM_COTIZACIONVENTA] COTIZ WITH(NOLOCK) ON COTIZ.ID_COTIZACION = COTDET.ID_COTIZACION
		LEFT JOIN [dbo].[TBM_SOLICITUDVENTA] SOL WITH(NOLOCK) ON SOL.ID_SOLICITUD = COTIZ.ID_SOLICITUD
		LEFT JOIN [dbo].[TBM_COTDET_DESPACHO] COTDESP WITH(NOLOCK) ON COTDESP.ID_COTDETALLE = COTDET.ID
		LEFT JOIN [dbo].[TBD_DATOS_GENERALES] AS DATOS WITH(NOLOCK) ON DATOS.DOMINIO = 'GARANTIAS' AND DATOS.PARAMETRO = COTIZ.GARANTIA AND DATOS.ESTADO = '1'
		WHERE MANT.ID_MANT = @IsNumMant


/***************************************************************************************************************/

/*****Obtenemos el detalle del equipo **************************************************************************/
	SELECT
		parcial.ID_MANT
		,parcial.RAZONSOCIAL
		,parcial.CODITEM
		,parcial.DESCRIPCION
		,RGA.[DESMARCA]
		,parcial.FECHAINSTALACION
		,'MODELO' AS MODELO --PENDIENTE REVISAR.
		,parcial.SERIE
		,parcial.DIRECCION
		,CONCAT(UBI.NOMDEPARTAMENTO,' / ',UBI.NOMPROVINCIA,' / ',UBI.NOMDISTRITO) AS UBIGEODEST
		,parcial.UBIGEODEST AS CODUBIGEODEST
		,parcial.NUMFIANZA
		,CASE WHEN parcial.GARANTIAADIC IS NULL
		THEN parcial.FECHAVENCIMIENTOGAR
		ELSE	
			DATEADD(month,CAST((SUBSTRING(TRIM(DATOS.VALOR1),0,CHARINDEX(' ',DATOS.VALOR1,0))) AS INT) ,parcial.FECHAVENCIMIENTOGAR) 
		END AS FECHAVENCIMIENTOGAR
		,CIClO.DESCRIPCION AS PERIODO 
		,GARANTIA AS GARANTIA
		,DATOS.VALOR1 GARANTIAADIC
	INTO #tmpCuerpoEquipo
	FROM #tmpCabecera parcial
 	LEFT JOIN [dbo].[TBM_UBIGEO] UBI WITH(NOLOCK) ON UBI.CODUBIGEO = parcial.UBIGEODEST
	LEFT JOIN [dbo].[TBD_DATOS_GENERALES] CICLO WITH(NOLOCK) ON CICLO.DOMINIO = 'CICLOPREV' AND CICLO.PARAMETRO = parcial.PERIODO
	LEFT JOIN [dbo].[TBD_DATOS_GENERALES] AS DATOS WITH(NOLOCK) ON DATOS.DOMINIO = 'GARANTIAS' AND DATOS.PARAMETRO = parcial.GARANTIAADIC AND DATOS.ESTADO = '1'
	LEFT JOIN (SELECT [NOMPRODUCTO],[DESMARCA],[CODIGOPRODUCTO] FROM OPENQUERY([AH-SRV4],'
		SELECT
			A.AR_CCODIGO CODIGOPRODUCTO,
			A.AR_CDESCRI NOMPRODUCTO,
			D.TG_CDESCRI DESMARCA
		FROM  [RSFACCAR].[dbo].[AL0007ARTI] A WITH(NOLOCK)
		LEFT JOIN  [RSFACCAR].[dbo].[AL0007STOC] B WITH(NOLOCK) ON A.AR_CCODIGO=B.SK_CCODIGO
		LEFT JOIN  [RSFACCAR].[dbo].[AL0007TABL] D WITH(NOLOCK) ON D.TG_CCOD=''V7'' AND A.AR_CMARCA=D.TG_CCLAVE 
		LEFT JOIN  [RSFACCAR].[dbo].[AL0007SERI] F WITH(NOLOCK) ON A.AR_CCODIGO=F.SR_CCODIGO AND B.SK_CALMA=F.SR_CALMA AND F.SR_NSKDIS<>0 
		WHERE B.SK_CALMA IN (''0001'',''0015'',''0017'') GROUP BY A.AR_CCODIGO,A.AR_CDESCRI,D.TG_CDESCRI,F.SR_CSERIE' )) RGA ON RGA.CODIGOPRODUCTO = parcial.CODITEM
	ORDER BY ID_MANT ASC

/********************************************************************************************************************/

/****Lista de mantenimientos programados*****************************************************************************/
	SELECT 
		DETMANT.ID				AS ID_DETALLE
		,ID_MANT				AS ID_MANT
		,ID_WORKFLOW			AS ID_WORKFLOW
		,FECHAMANTENIMIENTO		AS FECHAMANTENIMIENTO
		,ESTADO					AS CODESTADO
		,ABREV_ESTADO			AS ESTADO
	INTO #tmpMantenimientos
	FROM [dbo].[TBD_MANT_PREV] DETMANT 
	LEFT JOIN [dbo].[TBM_PROCESOESTADOS] ESTADO ON ESTADO.COD_ESTADO = DETMANT.ESTADO AND ESTADO.ID_PROCESO = 6
	WHERE DETMANT.ID_MANT = @IsNumMant
	ORDER BY FECHAMANTENIMIENTO
/***************************************************************************************************************/

/*Select General*/
	SELECT ID_MANT,RAZONSOCIAL,NUMPROCESO,TIPOPROCESO,ORDENCOMPRA FROM #tmpCabecera

	SELECT 
		EQUIPO.*
		,DATEDIFF(DAY,GETDATE(), FECHAVENCIMIENTOGAR)		DIFDIAS
		,DATEDIFF(DAY, EQUIPO.FECHAINSTALACION, GETDATE()) DIASTRANCURRIDOS
		,TOTALPREVE
		,PENDIENTES
		,COMPLETADOS
		,PROXFECHAMANT
	FROM #tmpCuerpoEquipo EQUIPO
	LEFT JOIN #tmpMantPrev PREV ON PREV.ID_MANT = EQUIPO.ID_MANT
	LEFT JOIN #tmpProxavencer VENC ON VENC.SERIE = EQUIPO.SERIE

	SELECT * FROM #tmpMantenimientos

SET NOCOUNT OFF 
END