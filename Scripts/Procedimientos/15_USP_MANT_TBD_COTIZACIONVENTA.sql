USE [DB_AHSECO]
GO

CREATE OR ALTER PROCEDURE [dbo].[USP_MANT_TBD_COTIZACIONVENTA] 
(
	 @isTipoProceso			CHAR(1)
	,@isID					BIGINT
	,@isID_COTIZACION		BIGINT
	,@isNROITEM				INT
	,@isTIPOITEM			VARCHAR(5)
	,@isCODITEM				VARCHAR(35)
	,@isDESCRIPCION			VARCHAR(100)
	,@isDESCRIPADIC			VARCHAR(1000)
	,@isSTOCK				INT
	,@isINDSTOCK			CHAR(1)
	,@isUNDMED				VARCHAR(3)
	,@isCANTIDAD			INT
	,@isCOSTOFOB			DECIMAL(18,9)
	,@isVVENTAUNI			DECIMAL(18,9)
	,@isPORCGANANCIA		DECIMAL(18,9)
	,@isUsrEjecuta			VARCHAR(50)
	,@isFecEjecuta			DATETIME
)
/*=======================================================================================================
	Nombre:				Fecha:			Descripcion:
	Diego Bazalar		27.09.24		Realiza el mantenimiento de la tabla TBD_COTIZACIONVENTA con parametros.
  =======================================================================================================*/
AS
BEGIN
	DECLARE @CODDETALLE INT, @MSG VARCHAR(20)
	DECLARE @CANTREG INT
	DECLARE @SUBTOTALVENTA DECIMAL(18,9)
	DECLARE @SUBTOTALVENTADSCTO DECIMAL(18,9)
	DECLARE @PORCDSCTO DECIMAL(18,9)
	DECLARE @TOTALDSCTO DECIMAL(18,9)
	DECLARE @NUM INT
	SET NOCOUNT ON;

	IF (@isTipoProceso = 'I')
	BEGIN
		INSERT INTO TBD_COTIZACIONVENTA
		(ID_COTIZACION,NROITEM,TIPOITEM,CODITEM,DESCRIPCION,DESCRIPADIC,STOCK,INDSTOCK,UNDMED,CANTIDAD,COSTOFOB,VVENTAUNI,PORCGANANCIA,USR_REG,FEC_REG)
		VALUES
		(@isID_COTIZACION,@isNROITEM,@isTIPOITEM,@isCODITEM,@isDESCRIPCION,@isDESCRIPADIC,@isSTOCK,@isINDSTOCK,@isUNDMED,@isCANTIDAD,@isCOSTOFOB,@isVVENTAUNI,@isPORCGANANCIA,@isUsrEjecuta,@isFecEjecuta)
		SET  @CODDETALLE = @@IDENTITY
		SET @MSG ='Registro Insertado con éxito'
	END

	IF(@isTipoProceso = 'U')
	BEGIN

		UPDATE TBD_COTIZACIONVENTA
		SET 
		NROITEM = @isNROITEM
		,TIPOITEM = @isTIPOITEM
		,CODITEM = @isCODITEM
		,DESCRIPCION = @isDESCRIPCION
		,DESCRIPADIC = @isDESCRIPADIC		
		,STOCK = @isSTOCK
		,INDSTOCK = @isINDSTOCK
		,UNDMED = @isUNDMED
		,CANTIDAD = @isCANTIDAD			
		,COSTOFOB = @isCOSTOFOB
		,VVENTAUNI = @isVVENTAUNI
		,PORCGANANCIA = @isPORCGANANCIA
		,USR_MOD = @isUsrEjecuta		
		,FEC_MOD = @isFecEjecuta
		WHERE ID = @isID
		
		SELECT @CANTREG = COUNT(1) FROM TBD_COTIZACIONVENTA 
		WHERE ID_COTIZACION = @isID_COTIZACION;

		--Se actualiza el Total Sin IGV
		IF ISNULL(@isVVENTAUNI,0) > 0 BEGIN
			UPDATE TBD_COTIZACIONVENTA SET VVTOTALSIGV = ISNULL(CANTIDAD,0) * ISNULL(VVENTAUNI,0) WHERE ID = @isID
		END
		ELSE BEGIN
			UPDATE TBD_COTIZACIONVENTA SET VVTOTALSIGV = NULL WHERE ID = @isID
		END
		
		--Se actualiza el Total Sin IGV pero con Ganancia
		IF ISNULL(@isPORCGANANCIA,0) > 0 BEGIN
			UPDATE TBD_COTIZACIONVENTA SET 
			VVTOTALSIGVCGAN = VVTOTALSIGV + (VVTOTALSIGV * (PORCGANANCIA / 100)) 
			WHERE ID = @isID AND ISNULL(VVTOTALSIGV,0) > 0
		END
		ELSE BEGIN
			UPDATE TBD_COTIZACIONVENTA SET VVTOTALSIGVCGAN = NULL WHERE ID = @isID
		END
		
		--Se calcula el SUBTOTAL CON GANANCIA O SIN GANANCIA
		SELECT @SUBTOTALVENTA = SUM(CASE WHEN ISNULL(PORCGANANCIA,0) > 0 THEN ISNULL(VVTOTALSIGVCGAN,0) ELSE ISNULL(VVTOTALSIGV,0) END) 
		FROM TBD_COTIZACIONVENTA WHERE ID_COTIZACION = @isID_COTIZACION;

		UPDATE TBM_COTIZACIONVENTA SET SUBTOTALVENTA = @SUBTOTALVENTA, MONTOIGV = @SUBTOTALVENTA * 0.18
		WHERE ID_COTIZACION = @isID_COTIZACION;
		
		--Se calcula el total de la cotización CON DESCUENTO O SIN DESCUENTO
		SELECT TOP 1 @PORCDSCTO = PORCDSCTO FROM TBM_COTIZACIONVENTA WHERE ID_COTIZACION = @isID_COTIZACION;

		IF ISNULL(@PORCDSCTO,0) > 0 BEGIN

			SET @TOTALDSCTO = @SUBTOTALVENTA * @PORCDSCTO;
			
			UPDATE TBD_COTIZACIONVENTA SET MONTODSCTO = @TOTALDSCTO / @CANTREG WHERE ID_COTIZACION = @isID_COTIZACION;
			
			UPDATE TBD_COTIZACIONVENTA SET
			VVTOTALSIGVDSCTO = CASE 
								WHEN ISNULL(VVTOTALSIGVCGAN,0) > 0 THEN VVTOTALSIGVCGAN - MONTODSCTO 
								ELSE VVTOTALSIGV - MONTODSCTO
								END
			WHERE ID_COTIZACION = @isID_COTIZACION;
			
			SELECT @SUBTOTALVENTA = SUM(ISNULL(VVTOTALSIGVDSCTO,0)) 
			FROM TBD_COTIZACIONVENTA WHERE ID_COTIZACION = @isID_COTIZACION;
			
			UPDATE TBM_COTIZACIONVENTA SET SUBTOTALVENTA = @SUBTOTALVENTA, MONTOIGV = @SUBTOTALVENTA * 0.18
			WHERE ID_COTIZACION = @isID_COTIZACION;
			
		END
		ELSE BEGIN
			
			UPDATE TBD_COTIZACIONVENTA SET MONTODSCTO = NULL,VVTOTALSIGVDSCTO = NULL WHERE ID_COTIZACION = @isID_COTIZACION;

		END
		
		UPDATE TBM_COTIZACIONVENTA SET TOTALVENTA = ISNULL(SUBTOTALVENTA,0) + ISNULL(MONTOIGV,0) WHERE ID_COTIZACION = @isID_COTIZACION;
		
		--Se valida si se termino la valorizacion por parte de los Jefes
		SELECT @NUM = SUM(CASE ISNULL(CD.INDSTOCK,'') 
							WHEN 'S' THEN (CASE WHEN ISNULL(VVENTAUNI,0) > 0 THEN 1 ELSE 0 END) 
							ELSE (CASE WHEN ISNULL(COSTOFOB,0) > 0 AND ISNULL(VVENTAUNI,0) > 0 THEN 1 ELSE 0 END) 
							END) 
		FROM TBD_COTIZACIONVENTA CD
		INNER JOIN TBM_COTIZACIONVENTA C ON CD.ID_COTIZACION = C.ID_COTIZACION
		WHERE CD.ID_COTIZACION = @isID_COTIZACION;

		IF @NUM = 0 BEGIN
			UPDATE TBM_COTIZACIONVENTA SET INDVALORIZADO = NULL WHERE ID_COTIZACION = @isID_COTIZACION
		END
		ELSE BEGIN
			
			IF @CANTREG > @NUM BEGIN
				UPDATE TBM_COTIZACIONVENTA SET INDVALORIZADO = 'N' WHERE ID_COTIZACION = @isID_COTIZACION
			END
			ELSE BEGIN
				UPDATE TBM_COTIZACIONVENTA SET INDVALORIZADO = 'S' WHERE ID_COTIZACION = @isID_COTIZACION
			END
			
		END

		SET  @CODDETALLE = @isID
		SET @MSG ='Registro Actualizado con éxito'

	END

	IF(@isTipoProceso = 'D')
	BEGIN
		DELETE FROM TBD_COTIZACIONVENTA WHERE ID = @isID
		SET  @CODDETALLE = @isID
		SET @MSG ='Registro eliminado con éxito'
	END

	SELECT @CODDETALLE COD, @MSG MSG
	SET NOCOUNT OFF;
END