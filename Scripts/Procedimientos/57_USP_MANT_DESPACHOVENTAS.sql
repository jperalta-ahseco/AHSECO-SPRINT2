USE [DB_AHSECO]
GO

CREATE OR ALTER PROCEDURE USP_MANT_DESPACHOVENTAS
/*==================================================================================================
	NOMBRE:					FECHA:		DESCRIPCIÓN:
	José A. Peralta		14.11.24		Se realiza la gestión de despacho de la venta concretada.
	EXEC USP_MANT_DESPACHOVENTAS 'I',1,1,123,'representante ventas','XXXX123','20241101','20241130','JPERALTA'
  ==================================================================================================*/
@TIPO CHAR(1),
@CODSOLICITUD BIGINT,
@CODCOTIZACION BIGINT,
@ID_WORKFLOW BIGINT,
@NOMPERFIL VARCHAR(100),
@NUMRODEN VARCHAR(50),
@FECHAORDEN DATETIME,
@FECHAMAX DATETIME,
@STOCK CHAR(1),
@FECHAENTREGA DATETIME,
@NUMFACTURA VARCHAR(50),
@NUMGUIAREM VARCHAR(50),
@NUMPEDIDO VARCHAR(50),
@FECHAINGRESO DATETIME,
@ESTAPROB VARCHAR(3),
@OBSERVACION VARCHAR(1000),
@USRREG VARCHAR(50)
AS
BEGIN
	SET NOCOUNT ON;
	DECLARE @CODIGO BIGINT,@MSG VARCHAR(250) ,@NOMUSUARIO VARCHAR(250)
	SELECT @CODIGO=0,@MSG='No se pudo realizar ninguna accion'

	IF(@TIPO = 'I')
	BEGIN

		WITH STOCKS AS (
    		SELECT DISTINCT STOCK FROM TBD_COTIZACIONVENTA A WITH(NOLOCK)
			INNER JOIN TBM_COTIZACIONVENTA B WITH(NOLOCK) ON A.ID_COTIZACION=B.ID_COTIZACION
			WHERE B.ID_COTIZACION = @CODCOTIZACION
		)
		INSERT INTO TBM_DESPACHO (ID_SOLICITUD,ID_COTIZACION,STOCK,NUMORDEN,FECHAORDEN,FECHAMAX,USR_REG,FEC_REG)
		SELECT @CODSOLICITUD ID_SOLICITUD, 
					  @CODCOTIZACION ID_COTIZACION,
					  CASE WHEN STOCK > 0 THEN 'S'  ELSE 'N' END STOCK,
					  @NUMRODEN NUMORDEN,
					  @FECHAORDEN FECHAORDEN,
					  @FECHAMAX FECHAMAX,
					  @USRREG USR_REG,
					  GETDATE() FEC_REG
			FROM STOCKS;

		--CAMBIO DE ESTADO DE LA SOLICITUD: En proceso de Ventas: (PRVT) 
		UPDATE TBM_SOLICITUDVENTA 
			SET ESTADO='PRVT'
			WHERE ID_SOLICITUD=@CODSOLICITUD;

		--SE REGISTRA LOG DE CAMBIO DE ESTADO:

		INSERT INTO TBM_WORKFLOWLOG(ID_WORKFLOW,COD_ESTADO,CARGO,AUDIT_REG_USR,AUDIT_REG_FEC)
		VALUES(@ID_WORKFLOW,'PRVT',@NOMPERFIL,@USRREG,GETDATE());

	
		SET  @CODIGO = 1
		SET @MSG ='Se realizó el registro de despacho con éxito'

	END

	IF(@TIPO = 'U')
	BEGIN
		
		UPDATE TBM_DESPACHO
		SET NUMORDEN = @NUMRODEN,
				FECHAORDEN = @FECHAORDEN,
				FECHAMAX = @FECHAMAX,
				USR_MOD = @USRREG,
				FEC_MOD = GETDATE()
				WHERE ID_SOLICITUD = @CODSOLICITUD;

		SET  @CODIGO = 1
		SET @MSG ='Se actualizó el registro de despacho con éxito'

	END

	IF(@TIPO = 'P')
	BEGIN
		
		UPDATE TBM_DESPACHO
			SET FECHAENTREGA =@FECHAENTREGA,
				   NUMFACTURA = @NUMFACTURA,
				   NUMGUIAREM = @NUMGUIAREM,
				   NUMPEDIDO = CASE WHEN @STOCK = 'N' THEN @NUMPEDIDO ELSE NUMPEDIDO END,
				   FECHAINGRESO = CASE WHEN @STOCK = 'N' THEN @FECHAINGRESO ELSE FECHAINGRESO END,
				   USR_MOD = @USRREG,
				   FEC_MOD = GETDATE()
			WHERE ID_SOLICITUD= @CODSOLICITUD
				AND STOCK = @STOCK;
			
		SET  @CODIGO = 1
		SET @MSG ='Se actualizó el registro de despacho con éxito'
	END

	IF(@TIPO = 'A')
	BEGIN
		UPDATE TBM_DESPACHO
			SET OBSERVACION ='',
				   FECAPROB = GETDATE(),
				   ESTAPROB = 'APR',
				   USR_MOD = @USRREG,
				   FEC_MOD = GETDATE()
			WHERE ID_SOLICITUD= @CODSOLICITUD
				AND STOCK = 'N';

		SET  @CODIGO = 1
		SET @MSG ='Aprobación exitosa de la importación.'
	END

	IF(@TIPO = 'O')
	BEGIN
		UPDATE TBM_DESPACHO
			SET OBSERVACION =@OBSERVACION,
				   FECAPROB = GETDATE(),
				   ESTAPROB = 'OBS',
				   USR_MOD = @USRREG,
				   FEC_MOD = GETDATE()
			WHERE ID_SOLICITUD= @CODSOLICITUD
				AND STOCK = 'N';

		SELECT @NOMUSUARIO=RTRIM(ISNULL(NOMBRES,''))+' '+RTRIM(ISNULL(APELLIDOS,'')) 
		FROM TBM_SEGURIDAD_USUARIO
		WHERE UPPER(USUARIO) = UPPER(@USRREG);

		--SE REGISTRA LA OBSERVACION:
		INSERT INTO TBM_OBSERVACIONES(ID_WORKFLOW,ESTADO_INSTANCIA,OBSERVACION,NOMBRE_USUARIO,PERFIL_USUARIO,USR_REG,FEC_REG)
			VALUES(@ID_WORKFLOW,'PRVT','[Observación de Gerencia] - '+RTRIM(@OBSERVACION),@NOMUSUARIO,@NOMPERFIL,@USRREG,GETDATE());

		SET  @CODIGO = 1
		SET @MSG ='Se observo el pedido de importación'
	END
		SELECT @CODIGO COD ,@MSG MSG

		SET NOCOUNT OFF;

END